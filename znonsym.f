C
C
C
      subroutine nonsym(A,X,C,N,MLEFT,MRIGHT,EPS,NCOND)
      implicit integer (i-n), real*8 (a-h,o-z)
C     *************************************************
C
C     5.3  SOLVES A REAL VALUED NONSYMMETRIC LINEAR SYSTEM  A . X  =  C
C
C     VERSION 1               APRIL 1988       KA        LAUSANNE
C     VERSION 2               MAI 1992         AJ        LAUSANNE
C
C-----------------------------------------------------------------------
      DIMENSION   A((MLEFT+MRIGHT+1)*N),    X(N),    C(N)
CMPLX COMPLEX     A,       X,       C,        ZPIVOT,   ZSUM,     ZTOP
      REAL*8        A,       X,       C,        ZPIVOT,   ZSUM,     ZTOP
      DATA        IMESS  / 0 /
C-----------------------------------------------------------------------
C
C     A IS A BANDMATRIX OF RANK N WITH MLEFT/MRIGHT OFF-DIAGONAL
C     ELEMENTS TO THE LEFT/RIGHT.
C     :A: AND :C: ARE DESTROYED BY :NONCYM: 
C     AT THE RETURN :C: CONTAINS THE SOLUTION VECTOR, :X: THE SQUARE
C     ROOT OF THE DIAGONAL ELEMENTS
C
C     THE ELEMENTS ARE HORIZONTALLY NUMBERED ROW AFTER ROW
C
C     FOR EASY NUMBERING, ZERO ELEMENTS HAVE BEEN INTRODUCED IN THE
C     UPPER LEFT-HAND CORNER AND IN THE LOWER RIGHT-HAND CORNER.
C     THESE ELEMENTS
C     M U S T  B E  S E T  T O  Z E R O
C     BEFORE CALLING :NONCYM: 
C
C     EXAMPLE FOR NUMBERING   MLEFT=2,  MRIGHT=3
C     -------
C
C     A(1)   A(2) I A(3)   A(4)   A(5)   A(6)
C     A(7) I A(8)   A(9)   A(10)  A(11)  A(12)
C     I                 .
C     I                        .
C
C     A PIVOT IS CONSIDERED TO BE BAD IF IT IS BY A FACTOR :EPS: SMALLER
C     THAN ITS OFF-DIAGONAL ELEMENTS. IF A BAD PIVOT IS ENCOUNTERED
C     A MESSAGE IS ISSUED (AT MOST TEN TIMES IN A RUN).
C     THE FLAG :NCOND: IS SET TO -1, OTHERWISE 0. 
C
C-----------------------------------------------------------------------
CL    0.        INITIALIZATION
C
      MOFFDI=MLEFT+MRIGHT
      MBAND=MOFFDI+1
      RATIO=0.
      NCOND=0
C     PRELIMINARY CHECK OF PIVOTS
      IPIV1=MLEFT+1
      IPIV2=IPIV1+(N-1)*MBAND
      DO 10 J=IPIV1,IPIV2,MBAND
CMPLX IF(CABS(A(J)) .EQ. 0.) GO TO 510
        IF( ABS(A(J)) .EQ. 0.) GO TO 510
 10   CONTINUE
C
C-----------------------------------------------------------------------
CL    1.        PRECONDITIONNING: GET ONES ON THE DIAGONAL
C
      DO 130 JP=1,N
        JPABS=(MLEFT+1)+(JP-1)*MBAND
CMPLX X(JP)=SQRT(CABS(A(JPABS)))
        X(JP)=SQRT(ABS(A(JPABS)))
C
C     DIVIDE THE EQUATIONS BY THE SQUARE ROOT OF THE PIVOT
        DO 110 J=-MLEFT,MRIGHT
          A(JPABS+J)=A(JPABS+J)/X(JP)
 110    CONTINUE
        C(JP)=C(JP)/X(JP)
C
C     CHANGE VARIABLES, DIVIDE COLUMN BY SQUARE ROOT OF THE PIVOT
        DO 120 JA=JPABS-MRIGHT*MOFFDI,JPABS+MLEFT*MOFFDI,MOFFDI
          IF (JA.GE.IPIV1 .AND. JA.LE.IPIV2) A(JA)=A(JA)/X(JP)
 120    CONTINUE
 130  CONTINUE
C
C-----------------------------------------------------------------------
CL    2.        CONSTRUCT UPPER TRIANGULAR MATRIX 
C
C     INITIALIZATION OF VERTICAL COUNTER
      IDOWN=MLEFT
C     PIVOT INDEX FOR FIRST INCOMPLETE COLUMN
      INCMPL=(N-MLEFT)*MBAND+MLEFT+1 
C     FIRST PIVOT AND LAST BUT ONE
      IPIV1=MLEFT+1
      IPIV2=IPIV1+(N-2)*MBAND
C
      DO 220 JPIVOT=IPIV1,IPIV2,MBAND
C     COLUMN LENGTH 
        IF(JPIVOT.GE.INCMPL) IDOWN=IDOWN-1
        ZPIVOT=A(JPIVOT)
C     FIRST AND LAST HORIZONTAL ELEMENT INDEX
        IHOR1=JPIVOT+1
        IHOR2=JPIVOT+MRIGHT
C     CHECK THE PIVOT
        ZMAX=0.
        DO 205 JHORIZ=IHOR1,IHOR2
CMPLX ZMAX=AMAX1(ZMAX,CABS(A(JHORIZ)))
c990131          ZMAX=AMAX1(ZMAX, ABS(A(JHORIZ)))
          ZMAX=MAX(ZMAX, ABS(A(JHORIZ)))
 205    CONTINUE
CMPLX IF(CABS(ZPIVOT).EQ.0.) GO TO 510
        IF( ABS(ZPIVOT).EQ.0.) GO TO 510
CMPLX RATIO=AMAX1(RATIO,ZMAX/CABS(ZPIVOT))
c990131        RATIO=AMAX1(RATIO,ZMAX/ ABS(ZPIVOT))
        RATIO=MAX(RATIO,ZMAX/ ABS(ZPIVOT))
C
        DO 210 JHORIZ=IHOR1,IHOR2
          ZTOP=-A(JHORIZ)/ZPIVOT
          A(JHORIZ)=ZTOP
C     INITIALIZATION OF ELEMENT INDEX
          IELEM=JHORIZ
C     INITIALIZATION OF RECTANGULAR RULE CORNER ELEMENT
          ICORN=JPIVOT
C     LOOP DOWN THE COLUMN
          DO 211 JDOWN=1,IDOWN 
            IELEM=IELEM+MOFFDI
            ICORN=ICORN+MOFFDI
            A(IELEM)=A(IELEM)+ZTOP*A(ICORN)
 211      CONTINUE
 210    CONTINUE
C     TREAT THE CONSTANTS
C     INDEX OF THE FIRST ONE
        ICONST=JPIVOT/MBAND+1
        ZTOP=-C(ICONST)/ZPIVOT
        C(ICONST)=ZTOP
C     INITIALIZATION OF RECTANGULAR RULE CORNER ELEMENT
        ICORN=JPIVOT
C     LOOP DOWN THE CONSTANTS 
        DO 221 JDOWN=1,IDOWN 
          ICONST=ICONST+1
          ICORN=ICORN+MOFFDI
          C(ICONST)=C(ICONST)+ZTOP*A(ICORN)
 221    CONTINUE
 220  CONTINUE
C
C-----------------------------------------------------------------------
CL    3.        BACKSUBSTITUTION
C
 300  CONTINUE
C     INITIALIZATION OF SOLUTION INDEX
      ISOLUT=N
C     LAST PIVOT
      JPIVOT=IPIV2+MBAND
C     CHECK LAST PIVOT
CMPLX IF(CABS(A(JPIVOT)).EQ.0.) RATIO=1.E100
c%OS  IF( ABS(A(JPIVOT)).EQ.0.) RATIO=1.E100
      IF( ABS(A(JPIVOT)).EQ.0.) go to 510
C     LAST UNKNOWN
      C(ISOLUT)=C(ISOLUT)/A(JPIVOT)
C     INITIALIZATION OF HORIZONTAL RANGE
      IHOR1=JPIVOT+1
      IHOR2=JPIVOT+MRIGHT
C
      INM1=N-1
      DO 320 J=1,INM1
        ISOLUT=ISOLUT-1
        IHOR1=IHOR1-MBAND
        IHOR2=IHOR2-MBAND
        ZSUM=-C(ISOLUT)
        IKNOWN=ISOLUT
        DO 310 JHORIZ=IHOR1,IHOR2
          IKNOWN=IKNOWN+1
          IF(IKNOWN.GT.N) GO TO 310
          ZSUM=ZSUM+A(JHORIZ)*C(IKNOWN)
 310    CONTINUE
        C(ISOLUT)=ZSUM
 320  CONTINUE
C
C-----------------------------------------------------------------------
CL    4.        PRECONDITIONNING: BACK TO ORIGINAL VARIABLES
C
      DO 410 J=1,N
        C(J)=C(J)/X(J)
 410  CONTINUE
C
C-----------------------------------------------------------------------
CL    5.        HAVE WE ENCOUNTERED A BAD PIVOT ? 
C
C%OS  IF(1./RATIO .GT. EPS) RETURN
      IF(RATIO .LT. 1./EPS) RETURN
      NCOND=-1
C     COUNT THE NUMBER OF MESSAGES ISSUED
      IMESS=IMESS+1
      IF(IMESS.LE.10) PRINT 9500, RATIO
      RETURN
C
C     ZERO PIVOT ENCOUNTERED
 510  CONTINUE
      PRINT 9510 
      STOP 'ZERO PIVOT IN NONCYM'
C
C-----------------------------------------------------------------------
CL    9.        FORMATS
C

 9500 FORMAT(/////20X,10(1H*),21H  MESSAGE FROM NONCYM,2X,10(1H*)///
     +  20X,'BAD PIVOT ENCOUNTERED, RATIO: ',1PE12.3///
     +  20X,43(1H*)///)
 9510 FORMAT(/////20X,10(1H*),'   ZERO PIVOT IN NONCYM'///)
C
      END
